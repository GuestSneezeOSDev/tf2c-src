// shadertype=hlsl
//========= ShiroDkxtro2's --ACROHS Ultimate Shaders Project-- ============//
//
//	Initial D.	:	23.01.2023 DMY
//	Last Change :	16.05.2023 DMY
//
//	Purpose of this File :	Bumped LightMappedGeneric Shader
//
//===========================================================================//

//===========================================================================//
//	Declare Static Combos
//===========================================================================//
// STATIC:	"ENVMAPMODE"				"0..2" // CUBEMAPS 2 0
// STATIC:	"PCC"						"0..1" // PARALLAXCORRECTEDCUBEMAPS 1 0
// STATIC:	"ENVMAPFRESNEL"				"0..1" // CUBEMAPS_FRESNEL 1 0
// STATIC:	"ENVMAPANISOTROPY"			"0..1"
// STATIC:	"SSBUMP"					"0..1" // SSBUMP 1 0
// STATIC:	"SELFILLUMMODE"				"0..3" // SELFILLUMTEXTURING 3 2
// STATIC:	"DETAILTEXTURE"				"0..1" // DETAILTEXTURING 1 0
// STATIC:	"BLENDTINTBYBASEALPHA"		"0..1" // BASETINTINGVIAALPHA 1 0
// STATIC:	"DESATURATEBYBASEALPHA"		"0..1"
// STATIC:	"PARALLAX"					"0..1" // PARALLAX_MAPPING 1 0

//===========================================================================//
//	Declare Dynamic Combos
//===========================================================================//
// DYNAMIC:	"PIXELFOGTYPE"				"0..1"
// DYNAMIC:	"WRITEWATERFOGTODESTALPHA"	"0..1"

//===========================================================================//
//	Skipping combos that are unneeded
//	GIGA IMPORTANT :	The order of the STATIC:'s and the order of the skips
//						will DRASTICALLY affect compiletime!
//===========================================================================//
// SKIP: ( $ENVMAPMODE == 0				&&	$PCC == 1 )
// SKIP: ( $ENVMAPMODE == 0				&&	$ENVMAPFRESNEL == 1 )
// SKIP: ( $ENVMAPMODE == 0				&&	$ENVMAPANISOTROPY == 1 )
// SKIP: ( $ENVMAPMODE != 2				&&	$SELFILLUMMODE == 2 )
// SKIP: ( $PCC == 1					&&	$ENVMAPANISOTROPY == 1 )
// SKIP: ( $ENVMAPANISOTROPY == 1		&&	$SSBUMP == 1)
// SKIP: ( $SELFILLUMMODE == 1			&&	$BLENDTINTBYBASEALPHA != 0 )
// SKIP: ( $BLENDTINTBYBASEALPHA != 0	&&	$DESATURATEBYBASEALPHA != 0 )
// SKIP: ( $PIXELFOGTYPE == 0			&&	$WRITEWATERFOGTODESTALPHA != 0 )

//===========================================================================//
//	Include Files ( and with them, function declarations )
//===========================================================================//

// This is used to disable features automatically.
#include "ACROHS_Defines.h"

// This IS a Shader used on BRUSHES
#define BRUSH

// Can use Seamless Texturemapping
#define SEAMLESS

// This is NOT a Shader used on MODELS
//#define MODEL

// This Shaders USES Normalmaps
#define NORMALTEXTURE

//	This has to be included first. It includes pragmas, #define's, and the register map.
#include "lux_common_ps_fxc.h"
// Lightmapping related functions
#include "lux_common_lightmapped.h"
// SelfIllum Data
#include "lux_common_selfillum.h"
// EnvMapping Data
#include "lux_common_envmap.h"
// Parallax Mapping functions
#include "lux_common_parallax.h"

//===========================================================================//
//	Everything Vertex Shader is outputting, enters here
//===========================================================================//
struct PS_INPUT
{
    float4	BaseDetailTexCoord		:	TEXCOORD0;
	float4	NormalEMMTexCoord		:	TEXCOORD1;
	float3	WorldNormal				:	TEXCOORD2;
	float3	WorldPos				:	TEXCOORD3;
	float3	ProjPos					:	TEXCOORD4;
	float4	LightmapTexCoord1And2	:	TEXCOORD5;
	float4	LightmapTexCoord3		:	TEXCOORD6;
	float4	SeamlessWCoordinates	:	TEXCOORD7; // UVW.z coordinates for Seamless Mapping
	float4	SeamlessWeights			:	COLOR0; // .w unused
};
 
//===========================================================================//
//	Rendering Start - Entry point
//===========================================================================//
float4 main(PS_INPUT i) : COLOR
{
	//	Getting our PS_INPUT... TODO/FIXME: Does this require more memory?
	//===========================================================================//
	// Integer Constants are for use with loops only...
	int iDetailBlendMode = trunc(f1DetailBlendMode);

	float3 f3BaseTextureUV = float3(i.BaseDetailTexCoord.xy, i.SeamlessWCoordinates.x);

	// Always need it.
	float3 f3NormalUV =  float3(i.NormalEMMTexCoord.xy, i.SeamlessWCoordinates.z);

	#if DETAILTEXTURE
	float3 f3DetailTextureUV = float3(i.BaseDetailTexCoord.zw, i.SeamlessWCoordinates.y);
	#endif

	#if (ENVMAPMODE == 2)
	float3 f3EnvMapMaskUV = float3(i.NormalEMMTexCoord.zw, i.SeamlessWCoordinates.w);
	#endif

	// .xyz's in case we need to pack something later
    float3 f3FaceNormal			= i.WorldNormal.xyz;
	float3 f3WorldPosition		= i.WorldPos.xyz;
	float3 f3ProjectPosition	= i.ProjPos.xyz;
	float4 f4LightmapUV1		= i.LightmapTexCoord1And2; // TODO: figure out what the heck is AA_CLAMP supposed to do
	float4 f4LightmapUV2		= i.LightmapTexCoord3;
	float4 f4SeamlessWeights	= i.SeamlessWeights;


	// ShiroDkxtro2 :	We do this like the PBR Shader.
	//					Tottery says this causes issues on smoothed models.
	//					However, it means we can receive more things from the Vertex Shader
	//					And I have not noticed any of such Issues.
	float3 f3SurfaceNormal = normalize(f3FaceNormal);
    float3 f3SurfaceTangent;
    float3 f3SurfaceBase; 
    float f1FlipSign;
    float3x3 f3x3NormalBasis = Compute_Tangent_Frame(f3SurfaceNormal, f3WorldPosition, f3NormalUV.xy , f3SurfaceTangent, f3SurfaceBase, f1FlipSign);
	float3	f3ViewVector = normalize(g_EyePos.xyz - f3WorldPosition); // Lo

#if PARALLAX
	float3 f3RelativeViewDirection	=	float3(	dot(f3ViewVector, f3SurfaceTangent),	dot(f3ViewVector, f3SurfaceBase),	dot(f3ViewVector, f3FaceNormal));

	// POM
	float2 Offset = ParallaxOcclusionMapping(f3BaseTextureUV.xy, f3RelativeViewDirection, Sampler_ParallaxMap, f1ParallaxHeight, f1ParallaxMaxOffset, 16, 0);

	// PIM
//	float2 Offset = ParallaxIntervalMapping(f3RelativeViewDirection, f1ParallaxHeight, f1ParallaxMaxOffset, Sampler_ParallaxMap, f3BaseTextureUV.xy, 16, 1);

	f3BaseTextureUV.xy	+= Offset;
	f3NormalUV.xy		+= Offset;

	#if (ENVMAPMODE == 2)
		f3EnvMapMaskUV.xy += Offset;
	#endif

	#if DETAILTEXTURE
		f3DetailTextureUV.xy += Offset;
	#endif
#endif

	//===========================================================================//
	//	Getting the current Pixels Color from the Samplers
	//===========================================================================//
		float4 f4BaseTexture;
		if(bSeamless_Base)
		{
			f4BaseTexture	= tex2D_Seamless(Sampler_BaseTexture, f3BaseTextureUV, f4SeamlessWeights);	
		}
		else
		{
			f4BaseTexture	= tex2D(Sampler_BaseTexture, f3BaseTextureUV.xy);	
		}

		float4	f4NormalTexture;
		if(bSeamless_Bump)
		{
			f4NormalTexture	= tex2D_Seamless(Sampler_NormalTexture, f3NormalUV, f4SeamlessWeights);	
		}
		else
		{
			f4NormalTexture	= tex2D(Sampler_NormalTexture, f3NormalUV.xy);	
		}
	//		( not on this shader	  ) Sampler_BaseTexture2
	//		( not on this shader	  ) Sampler_NormalTexture2
	#if DETAILTEXTURE
		float4	f4DetailTexture;
		if(bSeamless_Detail)
		{
			f4DetailTexture	= tex2D_Seamless(Sampler_DetailTexture, f3DetailTextureUV, f4SeamlessWeights);	
		}
		else
		{
			f4DetailTexture	= tex2D(Sampler_DetailTexture, f3DetailTextureUV.xy);
		}
	#endif

	#if (ENVMAPMODE == 2)
		float4	f4EnvMapMask;
		if(bSeamless_EnvMapMask)
		{
			f4EnvMapMask	= tex2D_Seamless(Sampler_EnvMapMask, f3EnvMapMaskUV, f4SeamlessWeights);	
		}
		else
		{
			f4EnvMapMask	= tex2D(Sampler_EnvMapMask, f3EnvMapMaskUV.xy);
		}
	#endif

	#if (SELFILLUMMODE == 1 || SELFILLUMMODE == 3)
		float4	f4SelfIllum		= tex2D(	Sampler_SelfIllum,			f3BaseTextureUV.xy);					
	#endif

	// This is the TextureNormal
	#if SSBUMP
		// No "decompression"!

			#if DETAILTEXTURE
				float3 f3LightMap = LUX_SSBumpedLightmap(f4NormalTexture.xyz, f4LightmapUV1, f4LightmapUV2, iDetailBlendMode, f4DetailTexture.xyz);
			#else
				float3 f3LightMap = LUX_SSBumpedLightmap(f4NormalTexture.xyz, f4LightmapUV1, f4LightmapUV2);
			#endif
		// Make the NormalTexture "Normal" so it can be used for other thingsS
		float3 f3NormalTexture	= normalize(LUX_BumpBasis[0].xyz * f4NormalTexture.xxx + LUX_BumpBasis[1].xyz * f4NormalTexture.yyy + LUX_BumpBasis[2].xyz * f4NormalTexture.zzz);
	#else
		float3 f3NormalTexture = normalize((f4NormalTexture.xyz * 2.0f - 1.0f ) * 2.0f); // Perturbed Normals
	#endif

	// This is a modified WorldNormal
//		f3NormalTexture.y *= f1FlipSign; // Fixup TextureNormal for ambient lighting
		f3FaceNormal = normalize(mul(f3NormalTexture, f3x3NormalBasis));

	//===========================================================================//
	//	Adding tints when possible
	//===========================================================================//

	#if BLENDTINTBYBASEALPHA
	// Same as VertexLitGeneric...
	float3	f4TintedBaseTexture	= f4BaseTexture.xyz * f3BaseTextureTint;
			f4TintedBaseTexture	= lerp(f4TintedBaseTexture, f3BaseTextureTint, f1BlendTintFactor);
			f4BaseTexture.xyz	= lerp(f4BaseTexture.xyz, f4TintedBaseTexture, f4BaseTexture.w);
	#else
			f4BaseTexture.xyz *= f3BaseTextureTint;
		#if DESATURATEBYBASEALPHA
			float3	f3DesaturatedBaseAlpha = dot(f4BaseTexture.xyz, f3LumCoefficients);
					f4BaseTexture.xyz = lerp(f4BaseTexture.xyz, f3DesaturatedBaseAlpha, f1DesaturationFactor);
		#endif
	#endif

	#if (SELFILLUMMODE == 3)
	f4SelfIllum.xyz *= f3SelfIllumTint;
	#endif

#if DETAILTEXTURE
	f4DetailTexture.xyz *= f3DetailTextureTint;
	if(iDetailBlendMode != 10) // 10 is DETAIL_SSBUMP_BUMP, where the DetailTexture is used on the Lightmap instead of Basetexture!
		f4BaseTexture = TextureCombine( f4BaseTexture, f4DetailTexture, iDetailBlendMode, f1DetailBlendFactor );
#endif

    float3 f3Result = {0.0f, 0.0f, 0.0f};

// We will compute lightmap color from our SSBump, it will return a "normal" normal texture we can use for our cubemap lookup.
#if !SSBUMP
	// Use FaceNormal and this will doom the resulting lightmap.
	#if DETAILTEXTURE
		float3 f3LightMap = LUX_BumpedLightmap(f3NormalTexture, f4LightmapUV1, f4LightmapUV2, iDetailBlendMode, f4DetailTexture.xyz);
		// If there is a DetailTexture, we want to pass DetailTexture to the Lightmap for Blendmode 10.
		// Otherwise we just do normal lightmapping code.
	#else
		float3 f3LightMap = LUX_BumpedLightmap(f3NormalTexture, f4LightmapUV1, f4LightmapUV2);
	#endif
	
#endif

// We need the lightwarp sampler for the parallax map.
#if !PARALLAX
if (bLightWarpTexture)
{
	f3LightMap = 2.0f * tex2D(Sampler_LightWarpTexture, float2( 0.5f * length(f3LightMap) , 0 ));
}
#endif
	
	f3Result = f4BaseTexture.xyz * f3LightMap;

	// Are we supposed to render a Cubemap?
#if (ENVMAPMODE > 0)

	float	f1NdotV		 = max(0, dot(f3FaceNormal, f3ViewVector)); // cosLo

	#if (ENVMAPMODE == 2) // $EnvMapMask
			float3	f3CubemapMask = f4EnvMapMask.xyz; // Already Blended
	#else
			float3 f3CubemapMask = lerp(float3(1.0f, 1.0f, 1.0f), f4BaseTexture.www, f1LerpBaseAlpha); 
			f3CubemapMask = lerp(f3CubemapMask, f4NormalTexture.www, f1LerpNormalAlpha);
	#endif

	#if ENVMAPANISOTROPY
		// EnvMap Anisotropy implementation adapted from Far Cry 4 / Filament, what did you think? CSGO?
		// Thank you Stephen McAuley, https://archive.org/details/GDC2015McAuley
		// Also thanks to Filament, https://google.github.io/filament/Filament.md.html

		float3 f3AnisotropicTangent = cross(f3ViewVector, f3FaceNormal);
		float3 f3AnisotropicNormal	= cross(f3FaceNormal, f3AnisotropicTangent);
		float3 BentNormal			= normalize(lerp(f3SurfaceNormal, f3AnisotropicNormal, f4NormalTexture.a * f1EnvMapAnisotropyScale));

		// ShiroDkxtro2 : Unlike Filament, do not invert f3ViewVector, if you do, the entire cubemap will be reversed!
		// I assume this comes from difference in their TangentSpace Orientation or the Coordinate System. ( XZY instead of XYZ? )
		// Anyways, This will result in a PROPERLY aligned Reflection Vector!
		float3 f3ReflectionVector	= reflect(f3ViewVector, BentNormal);
	#else
		// Use existing HLSL function for Reflection Vectors!
		float3 f3ReflectionVector = reflect(f3ViewVector, f3FaceNormal);
	#endif

#if (PCC == 1) // Are we doing Parallax Correction?
        //	Parallax correction (2_0b and beyond)
        //	Adapted from http://seblagarde.wordpress.com/2012/09/29/image-based-lighting-approaches-and-parallax-corrected-cubemap/
        float3 f3PositionLS = mul(float4(f3WorldPosition, 1), f4x3CorrectionMatrix);

        //	Not Tottery Magic Code
        //	float3	f3RayLS = mul(f3ReflectionVector, (float3x3) f4x3CorrectionMatrix);
        //	Tottery's Magic Code
		//	TODO: This uses a f4x3 but it should be using a float f3x3
		//	TODO: Stuff pos in the alpha if it isn't actually being used anyways.
        float3	f3RayLS = mul(float4(f3ReflectionVector, 0.0f), f4x3CorrectionMatrix);
        float3	f3FirstPlaneIntersect = (float3(1.0f, 1.0f, 1.0f) - f3PositionLS) / f3RayLS;
        float3	f3SecondPlaneIntersect = (-f3PositionLS) / f3RayLS;
        float3	f3FurthestPlane = max(f3FirstPlaneIntersect, f3SecondPlaneIntersect);
        float	f1Distance = min(f3FurthestPlane.x, min(f3FurthestPlane.y, f3FurthestPlane.z));

        // Use distance in WS directly to recover intersection
        float3 f3IntersectPositionWS = f3WorldPosition + f3ReflectionVector * f1Distance;

        f3ReflectionVector = f3IntersectPositionWS - f3CubeMapPos;

		// Lookup the Texture using f3ReflectionVector as the VectorUV for the Cubemap. ( which pixel it points at)
        float4 f4SpecularUV = float4(f3ReflectionVector, ENVMAPLOD);
        float3 f3SpecularLookUp = ENV_MAP_SCALE * texCUBE(Sampler_EnvironmentMap, f4SpecularUV).xyz;

#else

		// Lookup the Texture using f3ReflectionVector as the VectorUV for the Cubemap. ( which pixel it points at)
        float4 f4SpecularUV = float4(f3ReflectionVector, ENVMAPLOD);
		float3 f3SpecularLookUp = ENV_MAP_SCALE * texCUBE(Sampler_EnvironmentMap, f4SpecularUV).xyz;

#endif

		f3SpecularLookUp *= lerp(f3CubemapMask, 1.0f - f3CubemapMask, f1EnvMapMaskFlip);

		f3SpecularLookUp *= f3EnvMapTint;
		float3 f3LookupHighSaturated = f3SpecularLookUp * f3SpecularLookUp;
		f3SpecularLookUp = lerp( f3SpecularLookUp, f3LookupHighSaturated, f1EnvMapContrast );

		// If you are curious this has to do with perception.
		float3 f3DesaturatedCubemap = dot( f3SpecularLookUp, float3( 0.299f, 0.587f, 0.114f ) );
		f3SpecularLookUp = lerp( f3DesaturatedCubemap, f3SpecularLookUp, f3EnvMapSaturation );

#if ENVMAPFRESNEL

	// TODO: Consider doing Unreal Engine Fresnel based on Envmapmask
	float f1Fresnel = 1.0f - f1NdotV;
	f1Fresnel = f1EnvMapFresnelScale * pow( f1Fresnel, f1EnvMapFresnelExponent) + f1EnvMapFresnelBias;
	f3SpecularLookUp *= f1Fresnel;

#endif

	f3Result += lerp(f3SpecularLookUp, f3SpecularLookUp * saturate(f3LightMap), f1EnvMapLightScale);
#endif // End Cubemap

    float f1FogFactor = CalcPixelFogFactor(PIXELFOGTYPE, g_FogParams, g_EyePos.xyz, f3WorldPosition, f3ProjectPosition.z);

#if WRITEWATERFOGTODESTALPHA && (PIXELFOGTYPE == PIXEL_FOG_TYPE_HEIGHT)
    float f1Alpha = f1FogFactor;
#else
    float f1Alpha = f4BaseTexture.a;
#endif // WRITEWATERFOGTODESTALPHA && (PIXELFOGTYPE == PIXEL_FOG_TYPE_HEIGHT)

	// Cannot do bDepthToDestAlpha && (WRITEWATERFOGTODESTALPHA == 0)
	// Boolean Registers may only be used in If-Statements!!!
	bool bWriteDepthToAlpha = false;
	#if WRITEWATERFOGTODESTALPHA
		bWriteDepthToAlpha = false;
	#endif

	if(!bDepthToDestAlpha)
	{
		bWriteDepthToAlpha = false;
	}

#if DETAILTEXTURE
	f3Result = TextureCombinePostLighting( f3Result, f4DetailTexture, iDetailBlendMode, f1DetailBlendFactor ); // $detail, $detailblendmode $detailblendfactor
#endif

// I think the order in which we add this doesn't matter so its at the end
// If statements should ever-so-slightly accelerate. By skipping pixels that get nothing added
// TODO: consider doing additive selfillumination instead. Maybe min() it though to not saturate...
// TODO: consider raising 0 to 0.001 for when something isn't "truly" black
#if (SELFILLUMMODE == 1) // Basealpha Selfillum and mask texture
		if (f4BaseTexture.a > 0)
		{
			f4SelfIllum.xyz = lerp( f4BaseTexture.aaa, f4SelfIllum.xyz, f1SelfIllumMaskFactor);
			f3Result.xyz = lerp(f3Result.xyz, f4BaseTexture.xyz * f3SelfIllumTint, f4SelfIllum.xyz);
		}
#elif ((SELFILLUMMODE == 2) && (ENVMAPMODE == 2))
		if(f4EnvMapMask.w > 0)
		{
			f3Result.xyz = lerp(f3Result.xyz, f4BaseTexture.xyz * f3SelfIllumTint ,f4EnvMapMask.w);
		}
#elif (SELFILLUMMODE == 3)
			f3Result.xyz += f4SelfIllum.xyz;
#endif // Selfillummode

	// $VertexColor
	if(bVertexColor)
	{
		f3Result.xyz *= i.SeamlessWeights.xyz;
	}

	// $VertexAlpha
	if(bVertexAlpha)
	{
		f1Alpha *= i.SeamlessWeights.w;
	}

    return FinalOutput(float4(f3Result.xyz, f1Alpha), f1FogFactor, PIXELFOGTYPE, TONEMAP_SCALE_LINEAR, bWriteDepthToAlpha, f3ProjectPosition.z);
}